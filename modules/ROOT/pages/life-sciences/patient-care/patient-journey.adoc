= Patient Journey

A patient journey is a representation of the journey a patient takes through the healthcare system. It is a graph that represents the nodes and relationships between the nodes.

== 1. Introduction

A patient journey is a representation of the journey a patient takes through the healthcare system. It is a graph that represents the nodes and relationships between the nodes.

Patient journey data is inherently both highly heterogeneous and deeply interconnected, spanning clinical records, laboratory results, medications, imaging, lifestyle factors, and social determinants of health. By definition, these diverse data points form a complex web of relationships, i.e. a patient's dietary habits influence metabolic conditions, exercise patterns correlate with cardiovascular outcomes, and environmental exposures connect to respiratory diseases. Traditional relational databases struggle to capture and query these intricate, multi-dimensional connections efficiently, often requiring complex joins across dozens of tables. Neo4j's flexible, schema-optional graph database model excels in this domain by naturally representing patients, conditions, treatments, and lifestyle factors as nodes with relationships that mirror real-world clinical causality and correlation. This approach enables healthcare organizations to seamlessly integrate disparate data sources while maintaining the semantic richness essential for comprehensive patient journey analytics.

== 2. Scenario

A patient journey is a representation of the journey a patient takes through the healthcare system. It is a graph that represents the nodes and relationships between the nodes.

.An example of a Patient Journey
image::life-sciences/patient-journey/patient_journey.png[width=800]

== 3. Solution

A patient journey use case tracks an individual's complete healthcare experience across multiple touchpoints, providers, and treatments over time. Neo4j's graph database excels at connecting fragmented patient data from disparate EHRs, lab results, prescriptions, and specialist visits—into a unified, relationship-rich model. This approach enables healthcare organizations to visualize complex care pathways, identify treatment gaps, and understand how different interventions impact outcomes. Graph queries can instantly traverse a patient's entire medical history to surface patterns like medication interactions, referral bottlenecks, or care coordination issues that traditional databases struggle to uncover. Ultimately, Neo4j empowers clinicians and administrators to deliver more personalized, continuous care while optimizing resource allocation across the healthcare network.

=== 4.1. Data Model

Neo4j's labeled property graph model naturally aligns with the https://www.ohdsi.org/data-standardization/[OMOP] Common Data Model by representing clinical concepts as nodes (`Person`, `Condition`, `Drug`, `Procedure`), their attributes as properties (dates, codes, values), and clinical events as relationships (`HAS_CONDITION`, `PRESCRIBED_DRUG`), enabling intuitive mapping of standardized observational health data while preserving the rich semantic connections between patients, diagnoses, treatments, and outcomes that OMOP vocabularies define.

==== 4.1.1 Overview

.Patient Journey model overview
image::life-sciences/patient-journey/Industry_Use_Cases_Patient_Journey.svg[width=800]

==== 4.1.2 Nodes

The following nodes are used in the Patient Journey data model:
NB: What is _required_ for the queries to work is listed below, but in the example data you will see additional properties to make it clearer - e.g. `name` on the `Drug` node.

`Patient` Node:

- `id`: Unique identifier for the patient
- `name`: Name of the patient

`Encounter` Node:

- `id`: Unique identifier for the encounter

`Observation` Node:

- `id`: Unique identifier for the observation
- `code`: Standardized code for the observation (e.g., LOINC)

`Provider` Node:

- `id`: Unique identifier for the provider

`Speciality` Node:

- `name`: Name of the speciality

`Organisation` Node:

- `id`: Unique identifier for the organisation

`Drug` Node:

- `code`: Standardized code for the drug (e.g., RxNorm)
- `name`: Name of the drug

`Condition` Node:

- `code`: Standardized code for the condition (e.g., SNOMED CT)
- `description`: Description of the condition

=== 4.2. Demo Data

The following Cypher statement will create the example graph in the Neo4j database:

[source, cypher, role=noheader]
----
// MERGE patients
MERGE (pas:Patient {id: '987-223-1345', name: 'Alice Smith', birthDate: date('1975-03-22')})
MERGE (pct:Patient {id: '987-223-1346', name: 'Charlotte Tegreba', birthDate: date('1992-03-22')})
MERGE (paj:Patient {id: '987-783-1346', name: 'Alex Jerdaschl', birthDate: date('1986-11-14')})

// MERGE providers, specialities and organisation
MERGE (p1:Provider {id: 'prov-001', name: 'Dr. Emily Smith'})
MERGE (p2:Provider {id: 'prov-002', name: 'Dr. Anisha Jones'})
MERGE (p3:Provider {id: 'prov-078', name: 'Dr. Paul Vionnet'})

MERGE (sp1:Speciality {name: 'Cardiology'})
MERGE (sp2:Speciality {name: 'General Practice'})
MERGE (sp3:Speciality {name: 'Nephrology'})

MERGE (org1:Organisation {id: 'org-001', name: 'City Health Clinic', address: '123 Main St, Metropolis'})
MERGE (org2:Organisation {id: 'org-002', name: 'Centre for Kidney Health', address: '498 Main St, Metropolis'})

// MERGE provider relationships
MERGE (p1)-[:HAS_SPECIALITY]->(sp1)
MERGE (p2)-[:HAS_SPECIALITY]->(sp2)
MERGE (p3)-[:HAS_SPECIALITY]->(sp3)

MERGE (p1)-[:BELONGS_TO]->(org1)
MERGE (p2)-[:BELONGS_TO]->(org1)
MERGE (p3)-[:BELONGS_TO]->(org2)

//// Alice Smith encounters and data ////

// MERGE encounter 1 and associated data
MERGE (eas1:Encounter {id: 'enc-as75-001', date: date('2025-11-10'), type: 'Inpatient'})
MERGE (oas11:Observation {id: 'obs-as75-011', code: '8320-2', value: '175/80', unit: 'mmHg'})
MERGE (oas12:Observation {id: 'obs-as75-012', code: '29463-7', value: 68, unit: 'kg'})
MERGE (dras1:Drug {code:'386873009', name: 'Lisinopril', dosage: '10 mg', frequency: 'Once daily'})
MERGE (conas1:Condition {code: '38341003', description: 'Hypertensive disorder, systemic arterial (disorder)'})

// MERGE encounter 2 and associated data
MERGE (eas2:Encounter {id: 'enc-as75-002', date: date('2025-11-12'), type: 'Outpatient'})
MERGE (oas21:Observation {id: 'obs-as75-021', code: '8320-2', value: '140/80', unit: 'mmHg'})
MERGE (oas22:Observation {id: 'obs-as75-022', code: '29463-7', value: 68.1, unit: 'kg'})
MERGE (dras2:Drug {code:'386873009', name: 'Lisinopril', dosage: '10 mg', frequency: 'Once daily'})
MERGE (conas2:Condition {code: '44054006', description: 'Diabetes mellitus type 2 (disorder)'})

// MERGE encounter 3 and associated data
MERGE (eas3:Encounter {id: 'enc-as75-003', date: date('2025-11-24'), type: 'Outpatient'})
MERGE (oas31:Observation {id: 'obs-as75-031', code: '8320-2', value: '120/80', unit: 'mmHg'})
MERGE (oas32:Observation {id: 'obs-as75-032', code: '29463-7', value: 69.6, unit: 'kg'})

// MERGE encounter 1 relationships
MERGE (pas)-[:HAS_ENCOUNTER]->(eas1)
MERGE (eas1)-[:HAS_OBSERVATION]->(oas11)
MERGE (eas1)-[:HAS_OBSERVATION]->(oas12)
MERGE (eas1)-[:PRESCRIBED]->(dras1)
MERGE (eas1)-[:DIAGNOSED]->(conas1)
MERGE (eas1)-[:ATTENDED_BY]->(p1)

// MERGE encounter 2 relationships
MERGE (eas1)-[:NEXT]->(eas2)
MERGE (pas)-[:HAS_ENCOUNTER]->(eas2)
MERGE (eas2)-[:HAS_OBSERVATION]->(oas21)
MERGE (eas2)-[:HAS_OBSERVATION]->(oas22)
MERGE (eas2)-[:DIAGNOSED]->(conas1)
MERGE (eas2)-[:DIAGNOSED]->(conas2)
MERGE (eas2)-[:PRESCRIBED]->(dras2)
MERGE (eas2)-[:ATTENDED_BY]->(p2)

// MERGE encounter 3 relationships
MERGE (eas2)-[:NEXT]->(eas3)
MERGE (pas)-[:HAS_ENCOUNTER]->(eas3)
MERGE (eas3)-[:HAS_OBSERVATION]->(oas31)
MERGE (eas3)-[:HAS_OBSERVATION]->(oas32)
MERGE (eas3)-[:ATTENDED_BY]->(p2)

////  Charlotte Tegreba encounters and data ////

// MERGE encounter 1 and associated data
MERGE (ect1:Encounter {id: 'enc-ct92-001', date: date('2025-11-10'), type: 'Inpatient'})
MERGE (oct11:Observation {id: 'obs-ct92-011', code: '8320-2', value: '175/80', unit: 'mmHg'})
MERGE (oct12:Observation {id: 'obs-ct92-012', code: '29463-7', value: 68, unit: 'kg'})
MERGE (drct1:Drug {code:'386873009', name: 'Lisinopril', dosage: '10 mg', frequency: 'Once daily'})
MERGE (conct1:Condition {code: '38341003', description: 'Hypertensive disorder, systemic arterial (disorder)'})

// MERGE encounter 2 and associated data
MERGE (ect2:Encounter {id: 'enc-ct92-002', date: date('2025-11-12'), type: 'Outpatient'})
MERGE (oct21:Observation {id: 'obs-ct92-021', code: '8320-2', value: '140/80', unit: 'mmHg'})
MERGE (oct22:Observation {id: 'obs-ct92-022', code: '29463-7', value: 68.1, unit: 'kg'})
MERGE (drct2:Drug {code:'386873009', name: 'Lisinopril', dosage: '10 mg', frequency: 'Once daily'})
MERGE (conct2:Condition {code: '44054006', description: 'Diabetes mellitus type 2 (disorder)'})

// MERGE encounter 3 and associated data
MERGE (ect3:Encounter {id: 'enc-ct92-003', date: date('2025-11-24'), type: 'Outpatient'})
MERGE (oct31:Observation {id: 'obs-ct92-031', code: '8320-2', value: '120/80', unit: 'mmHg'})
MERGE (oct32:Observation {id: 'obs-ct92-032', code: '29463-7', value: 69.6, unit: 'kg'})

// MERGE encounter 1 relationships
MERGE (pct)-[:HAS_ENCOUNTER]->(ect1)
MERGE (ect1)-[:HAS_OBSERVATION]->(oct11)
MERGE (ect1)-[:HAS_OBSERVATION]->(oct12)
MERGE (ect1)-[:PRESCRIBED]->(drct1)
MERGE (ect1)-[:DIAGNOSED]->(conct1)
MERGE (ect1)-[:ATTENDED_BY]->(p1) 

// MERGE encounter 2 relationships
MERGE (ect1)-[:NEXT]->(ect2)
MERGE (pct)-[:HAS_ENCOUNTER]->(ect2)
MERGE (ect2)-[:HAS_OBSERVATION]->(oct21)
MERGE (ect2)-[:HAS_OBSERVATION]->(oct22)
MERGE (ect2)-[:DIAGNOSED]->(coct1)
MERGE (ect2)-[:DIAGNOSED]->(coct2)
MERGE (ect2)-[:PRESCRIBED]->(dct2)
MERGE (ect2)-[:ATTENDED_BY]->(p2)

// MERGE encounter 3 relationships
MERGE (e2ct)-[:NEXT]->(ect3)
MERGE (pct)-[:HAS_ENCOUNTER]->(ect)
MERGE (ect3)-[:HAS_OBSERVATION]->(oct31)
MERGE (ect3)-[:HAS_OBSERVATION]->(oct32)
MERGE (ect3)-[:ATTENDED_BY]->(p2)


//// Alex Jerdaschl encounters and data ////

// MERGE encounter 1 and associated data
MERGE (eaj1:Encounter {id: 'enc-aj86-001', date: date('2025-11-10'), type: 'Inpatient'})
MERGE (oaj11:Observation {id: 'obs-aj86-011', code: '8320-2', value: '173/80', unit: 'mmHg'})
MERGE (oaj12:Observation {id: 'obs-aj86-012', code: '29463-7', value: 60, unit: 'kg'})
MERGE (draj1:Drug {code:'703674001', name: 'Dapagliflozin', dosage: '10 mg', frequency: 'Once daily'})
MERGE (conaj2:Condition {code: '38341003', description: 'Hypertensive disorder, systemic arterial (disorder)'})
MERGE (con3:Condition {code: '709044004', description: 'Chronic kidney disease (disorder)'})

// MERGE encounter 2 and associated data
MERGE (eaj2:Encounter {id: 'enc-aj86-002', date: date('2025-11-12'), type: 'Outpatient'})
MERGE (oaj21:Observation {id: 'obs-aj86-021', code: '8320-2', value: '140/80', unit: 'mmHg'})
MERGE (oaj22:Observation {id: 'obs-aj86-022', code: '29463-7', value: 68.1, unit: 'kg'})
MERGE (draj2:Drug {code:'386873009', name: 'Lisinopril', dosage: '10 mg', frequency: 'Once daily'})

// MERGE encounter 1 relationships
MERGE (paj)-[:HAS_ENCOUNTER]->(eaj1)
MERGE (eaj1)-[:HAS_OBSERVATION]->(oaj11)
MERGE (eaj1)-[:HAS_OBSERVATION]->(oaj12)
MERGE (eaj1)-[:PRESCRIBED]->(draj1)
MERGE (eaj1)-[:DIAGNOSED]->(conaj3)
MERGE (eaj1)-[:ATTENDED_BY]->(p1) 

// MERGE encounter 2 relationships
MERGE (eaj1)-[:NEXT]->(eaj2)
MERGE (paj)-[:HAS_ENCOUNTER]->(eaj2)
MERGE (eaj2)-[:HAS_OBSERVATION]->(oaj21)
MERGE (eaj2)-[:HAS_OBSERVATION]->(oaj22)
MERGE (eaj2)-[:DIAGNOSED]->(conaj3)
MERGE (eaj2)-[:DIAGNOSED]->(conaj2)
MERGE (eaj2)-[:PRESCRIBED]->(draj2)
MERGE (eaj2)-[:ATTENDED_BY]->(p3)
----

=== 4.3. Schema

If you call:

[source, cypher, role=noheader]
----
// Show neo4j scheme
CALL db.schema.visualization()
----

You will see the following response:

image::life-sciences/patient-journey/patient-journey-schema.png[]

== 5. Cypher Queries

This section provides a set of example Cypher queries that can be used to explore the patient journey graph from different perspectives.

=== 5.1. Patient based queries

These queries are focused on retrieving information from a patient-centric perspective.

==== 5.1.1. Given a `Patient` what `Drug`s were `PRESCRIBED`?

[source, cypher, role=noheader]
----
// Get all the drugs a patient has been prescribed
MATCH (p:Patient {id: '987-223-1345'})-[:HAS_ENCOUNTER]->(:Encounter)-[:PRESCRIBED]->(d:Drug)
RETURN p.name AS Name, collect(DISTINCT d.name) AS `Prescribed Drugs`;
----

==== 5.1.2. Given a `Patient` what `Condition`s were `DIAGONOSED`?

[source, cypher, role=noheader]
----
// Get all the conditions a patient has been diagnosed with
MATCH (p:Patient {id: '987-223-1345'})-[:HAS_ENCOUNTER]->(:Encounter)-[:DIAGNOSED]->(c:Condition)
RETURN p.name AS Name, collect(DISTINCT c.description) AS conditions;
----

==== 5.1.3. Given a `Patient` and their `Condition` - what are the comorbidities of other `Patient`s with the same `Condition`?

[source, cypher, role=noheader]
----
MATCH 
    (p:Patient {id: '987-223-1345'})-[:HAS_ENCOUNTER]->(:Encounter)-[:DIAGNOSED]->(c:Condition),
    (c)<-[:DIAGNOSED]-(:Encounter)<-[:HAS_ENCOUNTER]-(other:Patient)-[:HAS_ENCOUNTER]->(:Encounter)-[:DIAGNOSED]->(c2:Condition)
WHERE c <> c2
RETURN c2.code AS Code, c2.description AS Description, count(DISTINCT other) AS PatientCount
ORDER BY PatientCount DESC;
----

=== 5.2. Condition based queries

These queries are focused on retrieving information from a condition-centric perspective.

==== 5.2.1. 

[source, cypher, role=noheader]
----
// What are the comorbidities associated with particular `Condition`?
MATCH (c1:Condition {code: '38341003'})<-[:DIAGNOSED]-(:Encounter)-[:DIAGNOSED]->(c2:Condition)
WHERE c1 <> c2
RETURN c2.code AS Code, c2.description AS Description, count(*) AS Occurrences
ORDER BY Occurrences DESC;
----

=== 5.3. Provider based queries

These queries are focused on retrieving information from a provider-centric perspective.

==== 5.3.1. Given a `Provider` what `Patient`s have they seen, and how often?

[source, cypher, role=noheader]
----
//* Given a provider what patients have they seen?
MATCH (p:Provider {id: 'prov-002'})<-[:ATTENDED_BY]-(:Encounter)<-[:HAS_ENCOUNTER]-(pt:Patient)
RETURN pt.id AS PatientId, pt.name AS `Patient Name`, count(*) AS Encounters
ORDER BY Encounters DESC;
----

==== 5.3.2. Given a `Provider` what `Condition`s have they `DIAGNOSED`?

[source, cypher, role=noheader]
----
// Given a provider what diagnoses have they made?
MATCH (p:Provider {id: 'prov-002'})<-[:ATTENDED_BY]-(:Encounter)-[:DIAGNOSED]->(c:Condition)
RETURN c.code AS Code, c.description AS Description, count(*) AS Diagnoses
ORDER BY Diagnoses DESC;
----

==== 5.3.3. Given a `Provider` what `Drug`s have they `PRESCRIBED`?

[source, cypher, role=noheader]
----
// Given a provider what drugs have they prescribed?
MATCH (p:Provider {id: 'prov-002'})<-[:ATTENDED_BY]-(:Encounter)-[:PRESCRIBED]->(d:Drug)
RETURN d.code AS Code, d.name AS Name, count(*) AS Prescriptions
----

==== 5.3.4. How are providers connected for a given `Patient`?

[source, cypher, role=noheader]
----
//TODO: Need more data to demo this properly
----

== 6. Graph Data Science (GDS)

Neo4j's Graph Data Science library enables sophisticated patient journey analytics using the information of the relationships. 

- Community detection algorithms identify sub-phenotypes of patients by clustering individuals with similar clinical trajectories, comorbidities, and treatment responses. 
- Centrality algorithms like PageRank and degree centrality can highlight critical entities within a patient's medical history—such as severe allergies, adverse drug reactions, or key diagnoses—that disproportionately influence care decisions and outcomes. 
- Pathfinding algorithms can reveal optimal treatment sequences by analyzing successful care pathways across similar patient cohorts, while similarity measures identify patients with comparable conditions for comparative effectiveness research. 
- Link prediction capabilities can forecast potential complications or disease progressions based on relationship patterns observed in historical patient data, enabling proactive intervention strategies.

== 7. Real World Examples

=== 7.1. AstraZeneca

AstraZeneca is using Neo4j to improve patient outcomes by analyzing patient journeys.

- https://neo4j.com/blog/healthcare/improving-patient-outcomes-algorithms-graphconnect/[Blog post]

.Improving Patient Outcomes with Graph Algorithms
video::6gAFnrWQ4kk[youtube]
